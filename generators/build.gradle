plugins {
	id "java-library"
	id "test-report-aggregation"
	id "jacoco"
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
	withJavadocJar()
	withSourcesJar()
}

tasks.sourcesJar {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = "io.github.dk96-os"
			artifactId = "generators"
			apply from: "../artifacts.gradle"
			version = artifactVersions.generators
			from components.java
		}
	}
}

dependencies {
	apply from: "../dependencies.gradle"

	api find_bugs

	api project(":lists")
	api project(":statistics")
	api project(":numbers")

	testImplementation platform(junit5.bom)
	testImplementation junit5.api
	testImplementation junit5.params
	testRuntimeOnly junit5.engine
}

sourceSets.main {
	java.srcDirs("src/main/java")
}

tasks.test {
	useJUnitPlatform() {
		excludeTags("slow")
	}
	maxParallelForks = 3
	testLogging {
		events("passed", "failed") // not including "skipped"
	}
	reports {
		junitXml {
			outputPerTestCase = true
			mergeReruns = true
		}
	}
	doLast {
		tasks.testAggregateTestReport
	}
}

tasks.jacocoTestReport {
	dependsOn tasks.test
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir("jacocoReport")
	}
}

tasks.jacocoTestCoverageVerification {
	dependsOn tasks.test
	violationRules {
		failOnViolation = true
		rule {
			limit {
				counter = "INSTRUCTION"
				minimum = 0.97
			}
			limit {
				counter = "BRANCH"
				minimum = 0.96
			}
		}
	}
}

tasks.check {
	dependsOn tasks.testAggregateTestReport
}
